plugins {
    id "architectury-plugin" version "3.4.135"
    id "dev.architectury.loom" version "0.12.0.282" apply false
    id "org.jetbrains.kotlin.jvm" version "1.6.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

def version_replaces = [
        "mod_id": rootProject.mod_id,
        "mod_version": rootProject.mod_version,
        "mod_name": rootProject.mod_name,
        "mod_description": rootProject.mod_description,
        "fabric_loader_version": rootProject.fabric_loader_version,
        "minecraft_version": rootProject.minecraft_version,
        "forge_version": rootProject.forge_version,
        "modmenu_version": rootProject.modmenu_version
]

subprojects {
    apply plugin: "dev.architectury.loom"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    }

    processResources {
        filesMatching("fabric.mod.json") {
            expand version_replaces
        }
        filesMatching("META-INF/mods.toml") {
            expand version_replaces
        }
    }
}


allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "org.jetbrains.kotlin.jvm"

    archivesBaseName = rootProject.archives_base_name
    group = 'me.lizardofoz.inventorio'
    version = rootProject.mod_version

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    compileKotlin.kotlinOptions.jvmTarget = "17"
    compileKotlin.kotlinOptions.freeCompilerArgs = ["-Xlambdas=indy"]

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}