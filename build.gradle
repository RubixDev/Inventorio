plugins {
    id "architectury-plugin" version "3.2.112"
    id "dev.architectury.loom" version "0.9.0.134" apply false
    id "org.jetbrains.kotlin.jvm" version "1.5.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

def version_replaces = [
        "mod_id": rootProject.mod_id,
        "mod_version": rootProject.mod_version,
        "mod_name": rootProject.mod_name,
        "mod_description": rootProject.mod_description,
        "fabric_loader_version": rootProject.fabric_loader_version,
        "minecraft_version": rootProject.minecraft_version,
        "forge_version": rootProject.forge_version
]

subprojects {
    apply plugin: "dev.architectury.loom"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        modImplementation("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}")
    }

    processResources {
        filesMatching("fabric.mod.json") {
            expand version_replaces
        }
        filesMatching("META-INF/mods.toml") {
            expand version_replaces
        }
    }
}


allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: "org.jetbrains.kotlin.jvm"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version

    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
    compileKotlin.kotlinOptions.jvmTarget = "16"
    compileKotlin.kotlinOptions.freeCompilerArgs = ["-Xlambdas=indy"]

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        if (JavaVersion.current().isJava9Compatible()) {
            options.release.set(16)
        }
    }

    java {
        withSourcesJar()
    }
}